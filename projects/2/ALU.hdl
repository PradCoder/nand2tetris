// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
      Mux16(a=x , b=false, sel=zx , out=op1x);
      Not16(in=op1x, out= op1nx);
      Mux16(a=op1x , b=op1nx, sel=nx , out=op2x);
      Mux16(a=y , b=false, sel=zy , out=op1y);
      Not16(in=op1y, out= op1ny);
      Mux16(a=op1y , b=op1ny, sel=ny , out=op2y);
      Add16(a=op2x,b=op2y, out= outaddxy);
      And16(a=op2x,b=op2y, out= outandxy);
      Mux16(a=outandxy, b=outaddxy, sel= f, out= op3xy);
      Not16(in= op3xy, out= op3nxy);
      Mux16(a=op3xy , b= op3nxy, sel= no, out= out, out[0..7]=low, out[8..15]=high,out[15]=ng);
      Or8Way(in= low, out= zr1);
      Or8Way(in= high, out= zr2);
      Or(a= zr1,b=zr2,out= zr3);
      Not(in=zr3,out=zr);
}
